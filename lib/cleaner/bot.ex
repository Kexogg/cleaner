defmodule Cleaner.Bot do
  @moduledoc false
  use ExGram.Bot, name: __MODULE__, setup_commands: true

  import Cleaner.BotUtils

  alias Cleaner.ChatConfig
  alias Cleaner.DelayMessageRemover

  command("ping", description: "–ü—Ä–æ–≤–µ—Ä–∏—Ç—å —Ä–∞–±–æ—Ç–∞–µ—Ç –ª–∏ –±–æ—Ç")
  command("help", description: "–í—ã–∑–≤–∞—Ç—å –ø–æ–º–æ—â—å")
  command("menu", description: "–í—ã–∑–≤–∞—Ç—å –º–µ–Ω—é")
  command("setdeletedelay", description: "–£—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –∑–∞–¥–µ—Ä–∂–∫—É –ø–µ—Ä–µ–¥ —É–¥–∞–ª–µ–Ω–∏–µ–º")

  middleware(ExGram.Middleware.IgnoreUsername)
  middleware(Cleaner.Middleware.FetchChat)
  middleware(Cleaner.Middleware.IsAdmin)

  @spec handle(ExGram.Dispatcher.parsed_message(), ExGram.Cnt.t()) :: ExGram.Cnt.t()
  def handle({:command, :ping, _message}, context) do
    answer_and_delete(context, "pong")
  end

  def handle({:command, :menu, _message}, context) do
    answer_and_delete(context, "/menu")
  end

  def handle({:command, :help, _message}, context) do
    help_text =
      Enum.random([
        "–ü–û–ú–ì–ê–Æ!!!",
        "–°—Ä–æ—á–Ω–æ –∑–≤–æ–Ω—é –≤ 112",
        "–ó–∞–≥—É–≥–ª–∏",
        "#–Ω–µ–æ—Å–∏–ª—è—Ç–æ—Ä",
        "–£ —á–∞—Ç–≥–ø—Ç —Å–ø—Ä–æ—Å–∏"
      ])

    answer_and_delete(context, help_text)
  end

  def handle({:command, :setdeletedelay, %{text: text}}, %{extra: %{chat_config: chat_config, admin?: true}} = context) do
    case ChatConfig.save(chat_config, %{delete_delay_in_seconds: text}) do
      {:ok, _chat_config} -> answer_and_delete(context, "–ì–æ—Ç–æ–≤–æ")
      {:error, _changeset} -> answer_and_delete(context, "–£–∫–∞–∂–∏—Ç–µ —á–∏—Å–ª–æ –±–æ–ª—å—à–µ 3")
    end
  end

  def handle({:command, :setdeletedelay, _message}, %{extra: %{admin?: false}} = context) do
    answer_and_delete(context, "–¢–û–õ–¨–ö–ê –î–õ–Ø –ê–î–ú–ò–ù–ê–í!!!")
  end

  def handle({:message, %{dice: dice} = message}, %{extra: %{chat_config: chat_config}} = context) do
    unless winning_dice?(dice) do
      DelayMessageRemover.schedule_delete_message(
        message.chat.id,
        message.message_id,
        chat_config.delete_delay_in_seconds
      )
    end

    context
  end

  def handle(_event, context), do: context

  defp winning_dice?(%{emoji: "üé∞", value: dice_value}) do
    <<right::binary-size(2), center::binary-size(2), left::binary-size(2)>> =
      (dice_value - 1)
      |> Integer.to_string(2)
      |> String.pad_leading(6, "0")

    left == center and center == right
  end

  @winning_value %{
    "üéØ" => 6,
    "üé≥" => 6,
    "üé≤" => 6,
    "‚öΩ" => 5,
    "üèÄ" => 5
  }

  defp winning_dice?(%{emoji: emoji, value: dice_value}) do
    %{^emoji => winning_value} = @winning_value
    winning_value == dice_value
  end
end
